<properties
 pageTitle="TS4 DC2 Extract information with kusto query to identify operation involved throttling"
 description="TS4 DC2 Extract information with kusto query to identify operation involved throttling"
 service="Microsoft.DocumentDB"
 resource="Microsoft.DocumentDB/databaseAccounts"
 authors="AzureData_AzureCosmosDB"
 ms.author="AzureData_AzureCosmosDB"
 displayOrder=""
 selfHelpType="TSG_Content"
 supportTopicIds=""
 resourceTags=""
 productPesIds=""
 cloudEnvironments="public, fairfax, usnat, ussec"
 articleId="002042f3-83b6-49ca-865d-ca72e6bb2efd"
 ownershipId="AzureData_AzureCosmosDB"
/>

# TS4 DC2 Extract information with kusto query to identify operation involved throttling

<br>
#### Step 1. Get an understanding of aggregated view of throttling(when was throttling happening most?) 
 
<pre>
BackendEndRequest5M
| where TIMESTAMP >= {startTime} and TIMESTAMP < {endTime}
| where DocumentServiceId startswith "<document service id of customer>" 
| where StatusCode == 429
| summarize sum(SampleCount) by bin(TIMESTAMP, 5m)
| order by TIMESTAMP desc
//| render timechart
</pre>
<br>

#### Step 2. Get the collections, partitioncounts, and RU limits for the account​
 
Run either of the following kusto query:
<br>
<br>**Query 1:**
<pre>
let subid = "{SubscriptionId}"; 
let accountname = "{Cosmos DB Account Name}"; 
ReportQuota5M 
| where TIMESTAMP >= {startTime} and TIMESTAMP < {endTime}
| where SubscriptionId == subid and GlobalDatabaseAccountName == accountname 
| summarize by PartitionId, CollectionName, CollectionRid, DocumentServiceId, Tenant 
| summarize count() by CollectionName, CollectionRid, DocumentServiceId, Tenant 
| summarize sum(count_), makelist(Tenant) by CollectionName, CollectionRid, DocumentServiceId 
| extend CountOfPartitions = sum_count_ | join kind=inner( OffersCapacity5Min 
| where TIMESTAMP > ago(1h) 
| where SubscriptionId == subid and GlobalDatabaseAccountName == accountname 
| extend PRU = iif(IsRUpm == 0, iif(ProvisionedThroughputCapacity != 0, ProvisionedThroughputCapacity, iif(OfferTypeWeight == 10, 250, iif(OfferTypeWeight == 20, 1000, 2500))), 0) 
| extend DocumentServiceId = DatabaseAccount 
| summarize max(PRU), max(IsRUpm), max(AllowWrite), max(AllowRead) by CollectionRid, DocumentServiceId ) on CollectionRid, DocumentServiceId 
| extend ProvisionedRU = max_PRU 
| extend RULimitPerPartition = ProvisionedRU/CountOfPartitions 
| extend RUPMEnabled = max_IsRUpm 
| extend RUPMLimitPerPartition = iif(RUPMEnabled==0, "NA", tostring(RULimitPerPartition*10)) 
| extend GeoAccessType = iff(max_AllowWrite == 1, "WriteRegion", "ReadRegion") 
| project DocumentServiceId, CollectionName, CollectionRid, CountOfPartitions, ProvisionedRU, RULimitPerPartition, RUPMEnabled, RUPMLimitPerPartition, list_Tenant, GeoAccessType
</pre>
<br>**Query 2:**
<pre>
let accountname = {globalDatabaseAccountName};
CollectionOverview(accountname)
|extend RULimitPerPartition = Throughput / TotalPartitionCount
|project Database, Collection, CollectionRid, IsPartitioned, TotalPartitionCount, Throughput, RULimitPerPartition, IsSharedThroughput, Autoscale, IndexVersion, IndexingMode, IsDefaultIndexingPolicy, MaxTotalUsageGB, TTL, MaxQuotaGB, MaxIndexUsageGB, MaxDocumentCount
</pre>
<br>

#### Step 3. Get the top throttled partition ids for the account
<pre>
BackendEndRequest5M
| where DocumentServiceId startswith <document service id of customer> and StatusCode == 429
| summarize sum(SampleCount) by PartitionId
</pre>
<br>

#### Step 4. Get involved Tenant and Collection rids
<pre>
BackendEndRequest5M 
| where DocumentServiceId startswith <document service id of customer> and PartitionId == "{partitionid}" 
| summarize sum(SampleCount) by StatusCode, CollectionRid, Tenant
</pre>
<br>

#### Step 5. Find out the RU consumption, RU Limit, throttled time periods, the operations done, and the throttled operations with the following query.

<pre>
let tenant= "{tenant}"; 
let partitionid = "{partitionid}"; 
let RULimit = RULimitPerPartition; // Reference “Get the collections, partitioncounts, and RU limits for the account​” to get RULimitPerPartition
BackendEndRequest5M 
| where Tenant == tenant and PartitionId == partitionid 
| extend RPS = SampleCount/300.0 | extend RU = TotalRequestCharge/300.0 
| summarize sum(RU), sum(RPS), max(ThroughputWeight), max(P50RequestCharge), max(P99RequestCharge), max(MaxRequestCharge), sum(SampleCount), makelist(strcat(OperationType, "-", ResourceType)) by bin(TIMESTAMP, 5m), RoleInstance, PartitionId, DocumentServiceId, Tenant
| extend RULimit = RULimit
| extend RULimitForReadOnly = RULimit/4
| extend OperationsDone = list_ 
| project TIMESTAMP, sum_RU, RULimitForReadOnly, RULimit, max_P50RequestCharge, max_P99RequestCharge, max_MaxRequestCharge, sum_SampleCount, sum_RPS, OperationsDone, RoleInstance, PartitionId, DocumentServiceId, Tenant
| join kind=inner( BackendEndRequest5M
| where Tenant == tenant and PartitionId == partitionid 
| extend ThrottledRequests = SampleCount
| extend TRPS = SampleCount/300.0 
| extend TRU = TotalRequestCharge/300.0 
| where StatusCode == 429
| summarize sum(ThrottledRequests), sum(TRPS), sum(TRU), makelist(strcat(OperationType, "-", ResourceType)) by bin(TIMESTAMP, 5m), RoleInstance, PartitionId, DocumentServiceId, Tenant 
| extend throttledOperations = list_ ) on TIMESTAMP, RoleInstance, PartitionId, DocumentServiceId, Tenant 
| extend successRPS = sum_RPS - sum_TRPS
| project TIMESTAMP, sum_RU, RULimitForReadOnly, RULimit, max_P50RequestCharge, max_P99RequestCharge, max_MaxRequestCharge, sum_SampleCount, sum_ThrottledRequests, sum_RPS,
sum_TRPS, successRPS, sum_TRU, OperationsDone, throttledOperations, RoleInstance, PartitionId, DocumentServiceId, Tenant
</pre>
<br>

#### Step 6. Check if throttling has occurred and identify the operation type involved throttling.
<br>If the sumThrottledRequests column is > 0, it means throttling has occurred. Else we need to check the start time and end time and the evidence of 429 again.
<br>
Look for the time periods, the roleinstance and the operation list to identify the operation type involved throttling.
<br>
<br>

#### Step 7. Check if rate limiting/throttling(429) was caused by either of the following known pattern one by one.
<br>
* **Pattern 1 - Throttling due to write operation**
<pre>
Pattern-matching Conditions:
1. Throttled operation is write operation
2. max_MaxRequestCharge > RULimit

Max request charge: max_MaxRequestCharge
RULimit for write: RULimit
</pre>
<br>

* **Pattern 2 - Throttling due to read operation**
<pre>
Pattern-matching Conditions:
1. Throttled operation is read operation
2. max_MaxRequestCharge > RULimitForReadyOnly?

Max request charge: max_MaxRequestCharge
RULimit for read only: RULimitForReadyOnly
</pre>
<br>

* **Pattern 3 - Throttling due to Avg RPS higher than what can be served**
<pre>
Pattern-matching Conditions:
. Sum_RPS > RULimit / max_MaxRequestCharge

Estimate from RU charges and RU Limit and the RPS that can actually be served without throttling.

Avg RPS: sum_RPS
RPS that can be served: RULimit / max_MaxRequestCharge
</pre>
<br>

* **Pattern 4 - Throttled due to exceeding the RU limit by single operation**
<pre>
Pattern-matching Conditions:
. Sum_RU > RULimit

request charge: sum_RU
RU Limit: RULimit
</pre>
<br>